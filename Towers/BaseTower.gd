extends Area2D
class_name BaseTower

const RED_TRANSPARENT_COLOR: Color = Color(1, 0, 0, 0.5)
const GREEN_TRANSPARENT_COLOR: Color = Color(0, 1, 0, 0.5)
const DEFAULT_COLOR: Color = Color(1, 1, 1, 1)

export (PackedScene) var Projectile
export var attack_range: float = 30.0
export var projectile_speed: float = 50.0
export var base_damage: int = 4
export var max_damage: int = 999999
export var damage: int = base_damage setget set_damage

onready var sprite: Sprite = $Sprite
onready var attackRange: Area2D = $AttackRange
onready var attackRangeCollider: CollisionShape2D = $AttackRange/CollisionShape2D
onready var fireBulletTimer: Timer = $FireBulletTimer
onready var animationPlayer: AnimationPlayer = $AnimationPlayer

var active: bool = false
export var draw_range: bool = false

func _ready() -> void:
	sprite.modulate = GREEN_TRANSPARENT_COLOR
	draw_range = true

func _process(delta: float) -> void:
	if animationPlayer.get_current_animation() != "Idle" && !animationPlayer.is_playing():
		animationPlayer.play("Idle")
	
func _physics_process(delta: float) -> void:
	if !active:
		return
	
	if fireBulletTimer.time_left <= 0:
		var target = find_target()
		if target != null:
			_fire(target)
	
	# Force _draw() to be called
	update()
	

func _draw() -> void:
	if draw_range || Utils.DRAW_DEBUG:
		draw_circle(Vector2.ZERO, attack_range, Color(0, 0, 1, 0.25))

func set_damage(value: int) -> void:
	damage = clamp(value, base_damage, max_damage)

func set_range_collider_size() -> void:
	if attackRangeCollider.shape is CircleShape2D:
		attackRangeCollider.shape.radius = attack_range

func build_tower() -> void:
	sprite.modulate = DEFAULT_COLOR
	active = true
	draw_range = false
	attackRangeCollider.disabled = false
	set_range_collider_size()
	fireBulletTimer.start()

# This returns a BaseEnemy, or null if none found
# TODO: make this not be so terribly inefficient.
func find_target():
	var overlappingAreas = attackRange.get_overlapping_areas()
	if overlappingAreas.empty():
		return null
	
	var maxUnitOffset: float = -1.0
	var target = null
	for area in overlappingAreas:
		var areaParent = area.get_parent()
		if areaParent is BaseEnemy && areaParent.unit_offset > maxUnitOffset:
			maxUnitOffset = areaParent.unit_offset
			target = areaParent
	
	return target

func _fire(target: BaseEnemy) -> void:
	_play_attack_animation()
	var projectile: BaseProjectile = Utils.instance_scene_on_main(Projectile, global_position)
	projectile.velocity = (target.global_position - global_position).normalized() * projectile_speed
	projectile.rotation = projectile.velocity.angle()
	projectile.set_damage(damage)
	projectile.z_index = z_index + 1
	fireBulletTimer.start()

# Virtual
func _play_attack_animation() -> void:
	pass

func set_cant_place_color() -> void:
	sprite.modulate = RED_TRANSPARENT_COLOR
	
func set_can_place_color() -> void:
	sprite.modulate = GREEN_TRANSPARENT_COLOR
